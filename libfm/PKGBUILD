# Maintainer: s8321414 <s8321414@chakraos.org>
# Arch Linux maintainers and contributors:
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgbase=libfm
pkgname=(libfm libfm-extra)
pkgver=1.2.5
pkgrel=1
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('x86_64')
license=('GPL')
depends=('menu-cache' 'desktop-file-utils' 'libexif' 'dbus-glib' 'shared-mime-info' 'vala')
makedepends=('udisks' 'intltool')
replaces=('lxshortcut')
provides=('lxshortcut')
conflicts=('lxshortcut')
source=(http://downloads.sourceforge.net/pcmanfm/libfm-$pkgver.tar.xz)
md5sums=('a1ba9ae5e920f38b647dd511edd6c807')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --without-gtk
  make
}

package_libfm() {
  depends+=('libfm-extra')
  install=libfm.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  # Temporary fix to FS#32361
  rm -rf "$pkgdir"/usr/include/libfm
  mv "$pkgdir"/usr/include/libfm-1.0/ "$pkgdir"/usr/include/libfm

  # Split libfm-extra
  [[ -d $srcdir/libfm-extra ]] && rm -r "$srcdir/libfm-extra/"
  mkdir "$srcdir"/libfm-extra
  mv "$pkgdir"/usr/include/libfm/fm-{extra,version,xml-file}.h \
     "$pkgdir"/usr/lib/libfm-extra.so* \
     "$pkgdir"/usr/lib/pkgconfig/libfm-extra.pc \
     "$srcdir/libfm-extra/"
}

package_libfm-extra() {
  pkgdesc="Extra library for file management" 
  depends=('glib2')
  replaces=()
  provides=()
  conflicts=()

  cd "$srcdir"/libfm-extra
  mkdir -p "$pkgdir"/usr/{include/libfm,lib/pkgconfig}
  mv *.h "$pkgdir/usr/include/libfm"
  mv libfm-extra.so* "$pkgdir/usr/lib"
  mv libfm-extra.pc "$pkgdir/usr/lib/pkgconfig"
}
